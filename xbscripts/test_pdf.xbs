/* 
 * Proyecto TPuy
 * test_pdf: Ejemplo de uso de TImprimePDF de Rafa.
 *
 */

#include "tpy_xbs.ch"
#include "harupdf.ch"
#include "tutilpdf.ch"

PROCEDURE test_pdf(  )
   Local oHaru, nLinea, samp_text, i, oFact
   LOCAL font_list  := { ;
                        "Courier",                  ;
                        "Courier-Bold",             ;
                        "Courier-Oblique",          ;
                        "Courier-BoldOblique",      ;
                        "Helvetica",                ;
                        "Helvetica-Bold",           ;
                        "Helvetica-Oblique",        ;
                        "Helvetica-BoldOblique",    ;
                        "Times-Roman",              ;
                        "Times-Bold",               ;
                        "Times-Italic",             ;
                        "Times-BoldItalic"         ;
                      }

   oHaru := TIMPRIMEPDF():New("test.pdf")                                  // Creamos documento

   oHaru:PageSetSize( HPDF_PAGE_SIZE_LETTER, HPDF_PAGE_LANDSCAPE ) // Page format

   UTILPDF oHaru:oUtil 1,1.5 ;
           IMAGE oTpuy:cImages+"tpuy-icon-64.png" //SIZE 8,5 

   UTILPDF oHaru:oUtil 2.5,4 ;
          SAY  "Proyecto TPuy" ;
          FONT "Times-Bold" ;
          SIZE 18 ;
          COLOR RGB 0.0, 0.0, 0.6


   oHaru:SetFont( font_list[1], 10)
   nLinea := 4
   oHaru:CmSay( nLinea, 1, "HARBOUR" )

   nLinea += 0.5
   oHaru:SetFont( font_list[1], 12 )
   oHaru:CMSAY( nLinea, 1, "power" )

   nLinea += 1
   oHaru:SetFont( font_list[5], 24 )

   oHaru:CMSAY( nLinea, 1, "(c)2011 by Rafa Carmona" )

   UTILPDF oHaru:oUtil BOX nLinea,13.25 TO nLinea + 1,14.8  FILLRGB 0.4,1,1
   UTILPDF oHaru:oUtil BOX nLinea,14.8  TO nLinea + 1,17    FILLRGB 0.4,1,1
   UTILPDF oHaru:oUtil BOX nLinea,17    TO nLinea + 1,19.5  FILLRGB 0.4,1,1


   //Second Page
   oHaru:Addpage()                                            // Add page to document
   oHaru:PageSetSize( HPDF_PAGE_SIZE_A4, HPDF_PAGE_PORTRAIT ) // Page format

   nLinea := 1
   FOR i := 1 TO Len( font_list )
      samp_text := "abcdefgABCDEFG12345!#$%&+-@?"
      oHaru:SetFont( font_list[i], 9 )
      oHaru:CMSAY( nLinea, 1, font_list[i] )
      nLinea += 0.6

      oHaru:SetFont( font_list[i], 20 )
      oHaru:CMSAY( nLinea, 1, samp_text  )
      nLinea += 0.6

   NEXT

   oHaru:End( .t. )

Return nil

