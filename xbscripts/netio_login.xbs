/*
 * netio_login: Realiza ingreso de usuario a sistema TPuy.
 */

#include "tpy_xbs.ch"

#xtranslate ::<exp>  =>  oForm:<exp>

function netio_login( lCheck )

   Local lConnect:=.f.
   Local oForm 

   DEFAULT lCheck := .t.

   if oTpuy:IsDef("WLogin") .AND. IsObject(oTpuy:WLogin) ; return nil ; endif

   DEFINE oForm PUBLIC

   if lCheck
      oTpuy:RunXBS("netio_check")
      if oTpuy:IsDef("oUser") .AND. IsObject(oTpuy:oUser)
         return .t.
      endif
   endif
   
   ::cResFile := oTpuy:cResources+"login.ui"

   SET RESOURCES ::oResource FROM FILE ::cResFile

   DEFINE WINDOW ::oWnd ;
          TITLE "Ingresar" ;
          OF oTpuy:oWnd ;
          ID "wnd_login" RESOURCE ::oResource

      DEFINE ENTRY ::oUser VAR ::cUser ;
             ID "user" RESOURCE ::oResource

      DEFINE ENTRY ::oPasswd VAR ::cPasswd ;
             ID "passwd" RESOURCE ::oResource

      DEFINE BUTTON ::oBtnConnect ;
             ACTION login_Run( oForm ) ;
             ID "btn_connect" RESOURCE ::oResource

      DEFINE BUTTON ::oBtnExit ;
             ACTION ( netio_disconnect(NETSERVER, NETPORT),;
                      log_exit(oForm), ::End() ) ;
             ID "btn_exit" RESOURCE ::oResource
   
      DEFINE BUTTON ::oBtnRegistro ;
             ACTION ( oTpuy:RunXBS("netio_registro", oForm ) );
             ID "btn_registro" RESOURCE ::oResource

   ACTIVATE WINDOW ::oWnd CENTER VALID log_exit(oForm)

   oTpuy:WLogin := ::oWnd


//   netio_disconnect( NETSERVER, NETPORT )
Return 

function log_exit(oForm)
   oTpuy:WLogin := nil
   oForm:=NIL
return .t.


function login_Run( oForm )
   Local cScript
   Local cLogin

   cLogin  := ::oUser:GetText()
   cScript := cLogin
   if empty(cScript) ; cScript := "netio_table" ; endif

   if Empty( ::oPasswd:GetText() )
      MsgAlert( "Debe incluir una contraseña.","Atención" )
      return .f.
   else
      //view(cLogin)
      if !net:tpy_IsLogin( cLogin )
         MsgAlert( "El valor de usuario [<b>" + cLogin + "]</b>. " + ;
                   "No es válido. ", "Valor Incorrecto" MARKUP )
         ::oUser:SetFocus()
         Return .f.
      else
         if !login_IsUser(oForm)
            return .f.
         endif
      endif

   endif

   DESTROY WINDOW ::oWnd

   oTpuy:oWLogin := nil
   oForm:Release()

   //oTpuy:RunXBS("net_poslogin")
   oTpuy:RunXBS("poslogin")
   
return .t.


function login_IsUser(oForm)
   local cUser, cPasswd 
   local cQuery, oMQuery
   local oUser,cAux

   cUser := ::oUser:GetText()
   cPasswd := hb_crypt( ::oPasswd:GetText(), oTpuy:cPassword )  //hb_MD5( ::oPasswd:GetText() )

   /* Si ya estaba un usuario registrado... entonces.. hay que cerrar la sesion. */
   if oTpuy:IsDef("oUser")
      if oTpuy:oUser:cLogin = cUser
         MsgInfo("Esta sesión corresponde al usuario ["+cUser+"]")
         return .f.
      endif
      TpyLogout( oTpuy:oUser:cLogin )
   endif

   /* oUser es el identificador del objeto remoto..
    * para enviar o recibir algo del objeto remoto usar:
    * ~~oUser:<mensaje> ó r:oUser:<mensaje>  */
   oUser := ~ServerLogin(cUser,cPasswd)
   if hb_IsNil( oUser ) 
      MsgStop("El usuario no existe")
      return .f. 
   endif

   if !oTpuy:IsDef( "oUser" ) 
      DEFINE PUBLIC oTpuy:oUser
      oTpuy:oUser:cHandle    := oUser

      oTpuy:oUser:cLogin     := r:oUser:cLogin
      oTpuy:oUser:cUserName  := r:oUser:cUserName 
      oTpuy:oUser:cShortName := r:oUser:cShortName
      oTpuy:oUser:tLastTime  := r:oUser:tLastTime
      oTpuy:oUser:cEMail     := r:oUser:cEMail
      oTpuy:oUser:cId        := r:oUser:cId
      oTpuy:oUser:cEntId     := r:oUser:cEntId
      oTpuy:oUser:lDeveloper := ~oServer:IsDeveloper()
   endif

//   oTpuy:oStatusBar:SetText("Usuario: "+ oTpuy:oUser:cShortName )

Return .t.


FUNCTION tpyLogout( cUser )
   local lRet := .f.
   local uValue, oMsgRun

   oMsgRun := MsgRunStart("Cerrando Objetos de la Sesión")

   inkey(.5)
   FOR EACH uValue IN oTpuy:oUser:hVars
      If hb_IsObject(uValue)
         uValue:End()
      EndIf
   NEXT 

   oTpuy:Del("oUser") 
tracelog("Eliminado el objeto oTpuy:oUser")
   lRet := ~ServerLogout( cUser )

   MsgRunStop(oMsgRun)
   
RETURN lRet


//EOF
